AC_INIT(src/Makefile.am)

AM_INIT_AUTOMAKE(devhelp, 0.5.0)
AM_CONFIG_HEADER(config.h)

AM_SANITY_CHECK
AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_CPP
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_LIBTOOL
AM_PATH_GLIB_2_0

dnl NOTE : it appears that some systems do not like it when you use line
dnl        continuation charactes to split CFLAGS into readable chunks
if test "x$GCC" = "xyes"; then
   CFLAGS="$CFLAGS -Wall -Wno-uninitialized -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith	 -Wno-sign-compare -Wsign-promo"
fi

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

AC_ARG_WITH(html-widget, [  --with-html-widget=[gtkhtml2/mozilla] html widget to use])


PKG_CHECK_MODULES(DEVHELP, [
	gtk+-2.0 >= 2.0.0])

PKG_CHECK_MODULES(LIBDEVHELP, [
        gtk+-2.0 >= 2.0.0
	libgsf-1 >= 1.4.0
	libgnomeui-2.0 >= 2.0.0
	gnome-vfs-2.0 >= 2.0.0
	libxml-2.0 >= 2.4.10
	libglade-2.0 >= 2.0.0
])


if test x$with_html_widget = xmozilla; then
	PKG_CHECK_MODULES(HTML_WIDGET, mozilla-gtkmozembed)
else
	PKG_CHECK_MODULES(HTML_WIDGET, libgtkhtml-2.0 >= 2.1.2)
fi

CFLAGS="$HTML_WIDGET_CFLAGS $CFLAGS"
AM_CONDITIONAL(HTML_WIDGET_MOZILLA, test x$with_html_widget = xmozilla)

PKG_CHECK_MODULES(EGG, [gtk+-2.0 >= 2.0.0])
AC_SUBST(EGG_CFLAGS)
AC_SUBST(EGG_LIBS)


dnl Checks for zlib library.
_cppflags="${CPPFLAGS}"
_ldflags="${LDFLAGS}"
 
AC_ARG_WITH(zlib,
[  --with-zlib[=DIR]       use libz in DIR],[
        if test "$withval" != "no" -a "$withval" != "yes"; then
                Z_DIR=$withval
                CPPFLAGS="${CPPFLAGS} -I$withval/include"
                LDFLAGS="${LDFLAGS} -L$withval/lib"
        fi
])
if test "$with_zlib" = "no"; then
        echo "Disabling compression support"
else
        AC_CHECK_HEADERS(zlib.h,
                AC_CHECK_LIB(z, gzread,[
                        AC_DEFINE(HAVE_LIBZ)
                        if test "x${Z_DIR}" != "x"; then
                                Z_CFLAGS="-I${Z_DIR}/include"
                                Z_LIBS="-L${Z_DIR}/lib -lz"
                                [case ${host} in
                                        *-*-solaris*)
                                                Z_LIBS="-L${Z_DIR}/lib -R${Z_DIR}/lib -lz"
                                                ;;
                                esac]
                        else
                                Z_LIBS="-lz"
                        fi]))
fi

AC_SUBST(Z_CFLAGS)
AC_SUBST(Z_LIBS)

CPPFLAGS=${_cppflags}
LDFLAGS=${_ldflags}

ALL_LINGUAS="am az be cs da de el es fr it ja kn lv ms nl no pl pt pt_BR ru sk sv uk vi zh_CN zh_TW"

GETTEXT_PACKAGE=devhelp
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

AM_GLIB_GNU_GETTEXT

AC_PROG_INTLTOOL

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

AC_OUTPUT([
Makefile
devhelp.spec
dtd/Makefile
libegg/Makefile
libegg/menu/Makefile
libegg/toolbar/Makefile
libegg/util/Makefile
po/Makefile.in
src/Makefile
ui/Makefile
])
